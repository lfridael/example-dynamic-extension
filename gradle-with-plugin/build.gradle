import com.github.dynamicextensionsalfresco.gradle.tasks.InstallBundle

plugins {
    id 'java'
    id 'idea'

    // Plugin used to be able to use the 'alfrescoProvided' dependency scope. 
    // See: https://github.com/xenit-eu/alfresco-gradle-sdk#the-euxenitalfresco-plugin
    id "eu.xenit.alfresco" version "0.2.0"

    // Plugin used to assemble the project into an Alfresco Module Package
    // See: https://github.com/xenit-eu/alfresco-gradle-sdk#the-euxenitamp-plugin
    id "eu.xenit.amp" version "0.2.0"

    // The Dynamic Extensions Gradle plugin
    // See: https://github.com/xenit-eu/dynamic-extensions-for-alfresco/blob/master/documentation/Gradle_Plugin.md
    id "eu.xenit.de" version "2.0.1"
}


description = 'Dynamic Extensions Example Web Scripts, Actions and Behaviours.'
version = '2.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "https://artifacts.alfresco.com/nexus/content/groups/public/"
    }
}

configurations {
    // Custom configuration added to demonstrate a custom InstallBundle task which can upload OSGi dependencies 
    // to Dynamic Extensions
    install
}

ext.alfrescoVersion = '5.1.g'

dependencies {
    alfrescoProvided "org.alfresco.surf:spring-webscripts:6.7"
    alfrescoProvided "org.alfresco:alfresco-data-model:${alfrescoVersion}"
    alfrescoProvided "org.alfresco:alfresco-repository:${alfrescoVersion}"

    // Note: SLF4J is already included in Alfresco, this is just included to demonstrate a custom installBundle task
    install 'org.slf4j:slf4j-api:1.7.26'
}

jar {
    bnd(
            'Bundle-Name': 'Example Dynamic Extension',
            'Bundle-Description': project.description,
            'Bundle-DocURL': 'https://github.com/xenit-eu/example-dynamic-extension',
            'Alfresco-Spring-Configuration': 'eu.xenit.de.example',

            // 'Public packages' should be in the 'Export-Package' MANIFEST header to be able to use them in other
            // OSGI bundles
            'Export-Package': 'eu.xenit.de.example.api'
    )
}

// Uncomment the alfrescoDynamicExtensions configuration block to overwrite the default settings

//alfrescoDynamicExtensions {
//    repository {
//        endpoint {
//            host = project.hasProperty('hostname') ? project.hostname : 'localhost'
//            port = '8080'
//            serviceUrl = '/alfresco/service'
//        }
//        authentication {
//            username = project.hasProperty('username') ? project.username : 'admin'
//            password = project.hasProperty('password') ? project.password : 'admin'
//        }
//    }
//    versions {
//        dynamicExtensions = '2.0.1'
//    }
//}

task installDependencies(type: InstallBundle) {
    files = configurations.install
}

wrapper {
    gradleVersion = "5.4.1"
}